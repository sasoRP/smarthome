val Functions$Function4<SwitchItem, SwitchItem, StringItem, Boolean, Void> handleShortPress =  [ SwitchItem lightBulbSwitch, SwitchItem toggleLightOnMotion, StringItem lightBulbAlert, Boolean enableToggleOnLightOff |
	logInfo("switches.rules", "handleShortPress()");
	
	if (lightBulbSwitch.state == OFF)
	{
		logInfo("switches.rules", "turning light ON and disabling toggleLightOnMotion...");
		sendCommand(lightBulbSwitch, ON);
		sendCommand(toggleLightOnMotion, OFF);
	}
	else
	{
		if (toggleLightOnMotion.state == OFF)
		{
			logInfo("switches.rules", "turning light OFF...");
			sendCommand(lightBulbSwitch, OFF);
			
			if (enableToggleOnLightOff)
			{
				logInfo("switches.rules", "enabling automatic mode...");
				sendCommand(toggleLightOnMotion, ON);
			}
		}
		else
		{
			logInfo("switches.rules", "disabling automatic mode...");
			sendCommand(toggleLightOnMotion, OFF);

			if (lightBulbAlert !== null) {
        		sendCommand(lightBulbAlert, "SELECT");
			} else {
				sendCommand(lightBulbSwitch, OFF);
				sendCommand(lightBulbSwitch, ON);
			}
		}
	}
]

val Functions$Function4<SwitchItem, DimmerItem, StringItem, SwitchItem, Void> handleDoublePress =  [SwitchItem lightBulbSwitch, DimmerItem lightBulbDimmer, StringItem lightBulbAlert, SwitchItem toggleLightOnMotion |
	logDebug("switches.rules", "handleDoublePress()");

	var shouldDisableMotionControl = (lightBulbSwitch.state == OFF);
	var shouldSetMaxBrightness = (lightBulbSwitch.state == ON);
	
	if (shouldDisableMotionControl)
	{
		if (lightBulbAlert !== null) {
			sendCommand(lightBulbAlert, "SELECT");
		} else {
			sendCommand(lightBulbSwitch, ON);
			sendCommand(lightBulbSwitch, OFF);
		}
		
		sendCommand(toggleLightOnMotion, OFF);
	}
	
	if (shouldSetMaxBrightness && lightBulbDimmer !== null)
	{
		lightBulbDimmer.sendCommand(100);
	}
]

val Functions$Function1<DimmerItem, Void> handleLongPress =  [DimmerItem lightBulbDimmer |
	logDebug("switches.rules", "handleLongPress()");

	var newBrightness = (lightBulbDimmer.state as DecimalType) + 20;
			
	if (newBrightness > 100) {
		newBrightness = 20;
	}
	
	lightBulbDimmer.sendCommand(newBrightness);
]


rule "Luz - Quarto"
when
    Channel "mihome:sensor_switch:286c07fa28af:158d00019dcdda:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			if (now.getHourOfDay > 0 && now.getHourOfDay < 8)
			{
				handleShortPress.apply(luz_quarto_switch, motion_quarto_enabled, luz_quarto_alert, false);
			}
			else
			{
				handleShortPress.apply(luz_quarto_switch, motion_quarto_enabled, luz_quarto_alert, true);
			}
        }
		
        case "DOUBLE_PRESSED": {
			handleDoublePress.apply(luz_quarto_switch, luz_quarto_brightness, luz_quarto_alert, motion_quarto_enabled);
        }
		
        case "LONG_PRESSED": {
			handleLongPress.apply(luz_quarto_brightness);
        }
		
        case "LONG_RELEASED": {
        }
    }
end


rule "Luz - Sala"
when
	Channel "mihome:sensor_switch:286c07fa28af:158d00019de580:button" triggered or 
	Channel "mihome:sensor_switch:286c07fa28af:158d00019dcc92:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			handleShortPress.apply(luz_sala_jantar_switch, motion_sala_enabled, luz_sala_jantar_alert, false);
			handleShortPress.apply(luz_sala_estar_switch, motion_sala_enabled, luz_sala_estar_alert, true);
        }
		
        case "DOUBLE_PRESSED": {
			handleDoublePress.apply(luz_sala_jantar_switch, luz_sala_jantar_brightness, luz_sala_jantar_alert, motion_sala_enabled);
			handleDoublePress.apply(luz_sala_estar_switch, luz_sala_estar_brightness, luz_sala_estar_alert, motion_sala_enabled);
        }
		
        case "LONG_PRESSED": {
			handleLongPress.apply(luz_sala_jantar_brightness);
			handleLongPress.apply(luz_sala_estar_brightness);
        }
		
        case "LONG_RELEASED": {
        }
    }
end


rule "Luz - Cozinha"
when
    Channel "mihome:sensor_switch_aq2:286c07fa28af:158d00012402f6:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			handleShortPress.apply(luz_cozinha_switch, motion_cozinha_enabled, null, true);
        }

		case "DOUBLE_PRESSED": {
			handleDoublePress.apply(luz_cozinha_switch, null, null, motion_cozinha_enabled);
        }
		
        case "LONG_RELEASED": {
        }
    }
end


rule "Luz - Hall Mesa"
when
	Channel "mihome:sensor_switch:286c07fa28af:158d00083a0508:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			handleShortPress.apply(luz_hall_mesa_switch, motion_hall_enabled, null, true);
        }
    }
end


rule "Luz - Corredor Mesa"
when
	Channel "mihome:sensor_switch:286c07fa28af:158d000839972e:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			handleShortPress.apply(luz_corredor_mesa_switch, motion_corredor_enabled, null, true);
        }
    }
end


rule "Luz - Suite 2"
when
	Channel "mihome:sensor_switch:286c07fa28af:158d000836b987:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			handleShortPress.apply(luz_suite_2_switch, motion_suite_enabled, null, true);
        }
		
        case "DOUBLE_PRESSED": {
			handleDoublePress.apply(luz_suite_2_switch, luz_suite_2_brightness, luz_suite_2_alert, null);
        }
		
        case "LONG_PRESSED": {
			handleLongPress.apply(luz_suite_2_brightness);
        }
		
        case "LONG_RELEASED": {
        }
    }
end